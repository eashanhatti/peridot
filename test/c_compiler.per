axiom CType: MetaType
axiom Int: CType

axiom CTerm: MetaFun(CType) -> MetaType
axiom zero: CTerm(Int)
axiom one: CTerm(Int)
axiom add: MetaFun(CTerm(Int), CTerm(Int)) -> CTerm(Int)
axiom var: MetaFun(Text) -> CTerm(Int)

axiom CStmt: MetaType
axiom return: MetaFun(CTerm(Int)) -> CStmt

axiom CDecl: MetaType
axiom func: MetaFun(CType, Text, Text, CType, CStmt) -> CDecl

prop CompileTerm(inf A: CType, CTerm(A), Text)
axiom compile_add:
  CompileTerm(add(`e1, `e2), "(" ++ `t1 ++ " + " ++ `t2 ++ ")")
  :- CompileTerm(`e1, `t1)
  ,  CompileTerm(`e2, `t2)
axiom compile_zero: CompileTerm(zero, "0")
axiom compile_one: CompileTerm(one, "1")
axiom compile_var: CompileTerm(var(`t), `t)

prop CompileDecl(CDecl, Text)
axiom compile_func:
  CompileDecl(
    func(`inTy, `f, `x, `outTy, `body),
    `cOutTy ++ " " ++ `f ++ "(" ++ `cInTy ++ " " ++ `x ++ ") { " ++ `cBody ++ " }")
  :- CompileType(`inTy, `cInTy)
  ,  CompileType(`outTy, cOutTy)
  ,  CompileStmt(`body, `cBody)

prop CompileType(CType, Text)
axiom compile_int: CompileType(Int, "int")

prop CompileStmt(CStmt, Text)
axiom compile_return:
  CompileStmt(return(`x), "return " ++ `t ++ ";")
  :- CompileTerm(`x, `t)

metavar output: Text
query CompileDecl(func(Int, "f", "x", Int, return(var("x"))), output)
output OUTPUT.c output
